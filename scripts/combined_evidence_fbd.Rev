###########################################
## Combined-evidence analysis of DNA and ##
## morphological data with the FBD model ##
##                   ---                 ##
##         EEOB 563 final project        ##
##       Bruno do Rosario Petrucci       ##
###########################################

###
# set up basic objects for the analysis

# taxa - this includes extant species, extinct species, and fossil 
# samples (named spMN for the (N + 1)th fossil of species spM
taxa <- readTaxonData("data/taxa.tsv")

# read nuclear DNA data
nuclear <- readDiscreteCharacterData("data/nuclear.nex")

# and morphological data
morpho <- readDiscreteCharacterData("data/morpho.nex")

# some quantities that we may like to use later
nTaxa <- taxa.size()
nBranches <- 2 * nTaxa - 3

# add missing data
nuclear.addMissingTaxa(taxa)
morpho.addMissingTaxa(taxa)

# set up moves and monitor vectors
moves = VectorMoves()
monitors = VectorMonitors()

###
# set up the fossilized-birth-death model

# birth and death rate priors
# mean taken from literature
birth ~ dnExponential(1 / 0.225)
death ~ dnExponential(1 / 0.120)

# moves - multiple tuning parameters to improve mixing
moves.append( mvScale(birth, lambda = 0.01, weight = 0.5) )
moves.append( mvScale(birth, lambda = 0.1, weight = 0.5) )
moves.append( mvScale(birth, lambda = 1, weight = 0.5) )

moves.append( mvScale(death, lambda = 0.01, weight = 0.5) )
moves.append( mvScale(death, lambda = 0.1, weight = 0.5) )
moves.append( mvScale(death, lambda = 1, weight = 0.5) )

# interesting quantities to keep track of as well
diversification := birth - death
turnover := death / birth

# probability of sampling extant lineages, we have 10/38
rho <- 10 / 38
# this includes recently extinct (< 1000y) as extant, to be clear

# fossil sampling rate - prior from my previous analysis
psi ~ dnExponential(1 / 0.79)

# moves
moves.append( mvScale(psi, lambda = 0.01, weight = 0.5) )
moves.append( mvScale(psi, lambda = 0.1, weight = 0.5) )
moves.append( mvScale(psi, lambda = 1, weight = 0.5) )

# origin time (root)
root_time ~ dnUniform(6, 30)
# a generous range between slightly older than the
# min age of the oldest fossil to slightly younger
# than all of caninae

# moves
moves.append( mvSlide(root_time, delta = 0.01, weight = 2) )
moves.append( mvSlide(root_time, delta = 0.1, weight = 2) )
moves.append( mvSlide(root_time, delta = 1, weight = 2) )

# set up final fbd tree
fbd_tree ~ dnFBDP(origin = root_time, lambda = birth, mu = death,
                  psi = psi, rho = rho, taxa = taxa)

# moves on topology and node ages
moves.append( mvFNPR(fbd_tree, weight = 7.5) )
moves.append( mvCollapseExpandFossilBranch(fbd_tree, root_time, weight = 3) )
moves.append( mvNodeTimeSlideUniform(fbd_tree, weight = 20) )
moves.append( mvRootTimeSlideUniform(fbd_tree, root_time, weight = 2) )

# now we need to create nodes to represent the likelihood for the fossil ages

# fossil occurrences
fossils = fbd_tree.getFossils()

# for each fossil
for (i in 1:fossils.size()) {
  # get time of the node
  t[i] := tmrca(fbd_tree, clade(fossils[i]))

  # and the minimum and maximum age of the fossil
  a_i = fossils[i].getMinAge()
  b_i = fossils[i].getMaxAge()

  # and create the likelihood as a uniform of t[i] between those clamped (observed) at 0
  F[i] ~ dnUniform(t[i] - b_i, t[i] - a_i)
  F[i].clamp(0)
}

# moves
moves.append( mvFossilTimeSlideUniform(fbd_tree, root_time, weight = 2) )

###
# molecular evolution model
# from model averaging, we found that a simple GTR model is the best

# base frequencies
pi ~ dnDirichlet( v(1, 1, 1, 1) )

# moves
moves.append( mvBetaSimplex(pi, alpha = 10, weight = 1) )
moves.append( mvSimplexElementScale(pi, alpha = 10, weight = 2) )

# exchangeability rates - transversions
er_transv ~ dnDirichlet( v(1, 1, 1, 1) )

# moves
moves.append( mvBetaSimplex(er_transv, alpha = 10, weight = 1) )
moves.append( mvSimplexElementScale(er_transv, alpha = 10, weight = 2) )

# create actual er vectors with transition rates 0
er := Simplex( v(er_transv[1], 0, er_transv[2], er_transv[3], 0, er_transv[4]) )

# create nuclear matrix
Q_nuclear := fnGTR(er, pi)

# branch rates - strict molecular clock since I did not do averaging on this
clock_nuclear ~ dnExponential(1)

# moves
moves.append( mvScale(clock_nuclear, lambda = 0.01, weight = 2) )
moves.append( mvScale(clock_nuclear, lambda = 0.1, weight = 2) )
moves.append( mvScale(clock_nuclear, lambda = 1, weight = 2) )

# phylogenetic CTMC for the nuclear data
phy_seq ~ dnPhyloCTMC(tree = fbd_tree, Q = Q_nuclear, branchRates = clock_nuclear, type = "DNA")

# clamp it to nuclear data
phy_seq.clamp(nuclear)

###
# morphological character evolution model
# from model averaging, the best model was F81

# max number of states
n_states <- 4

# start the vector of morpho restricted to a state sample size
morpho_bystate[1] <- morpho

# for each number of states from 2 to n_states
for (i in 2:n_states) {
  # copy morpho
  morpho_bystate[i] <- morpho

  # restrict it to characters with maximum state i
  morpho_bystate[i].setNumStatesPartition(i)

  # number of sites
  n_sites = morpho_bystate[i].nchar()

  # clock - also strict morphological clock since I did no averaging
  clock_morpho[i - 1] ~ dnExponential(1)

  # moves
  moves.append( mvScale(clock_morpho[i - 1], lambda = 0.01, weight = 2) )
  moves.append( mvScale(clock_morpho[i - 1], lambda = 0.1, weight = 2) )
  moves.append( mvScale(clock_morpho[i - 1], lambda = 1, weight = 2) )

  # set prior from discrete morphology tutorial
  er_scale[i - 1] ~ dnLognormal( 0.0, sd = 2 * 0.587405 )
  pi_scale[i - 1] ~ dnLognormal( 0.0, sd = 2 * 0.587405 )

  # moves
  moves.append( mvScale(er_scale[i - 1], lambda = 0.01, weight = 2) )
  moves.append( mvScale(er_scale[i - 1], lambda = 0.1, weight = 2) )
  moves.append( mvScale(er_scale[i - 1], lambda = 1, weight = 2) )

  moves.append( mvScale(pi_scale[i - 1], lambda = 0.01, weight = 2) )
  moves.append( mvScale(pi_scale[i - 1], lambda = 0.1, weight = 2) )
  moves.append( mvScale(pi_scale[i - 1], lambda = 1, weight = 2) )

  # set up Q
  Q[i - 1] := fnGTR( Simplex(fnDiscretizeBeta(er_scale[i - 1], er_scale[i - 1], sum(1:(i - 1)))),
                     Simplex(fnDiscretizeBeta(pi_scale[i - 1], pi_scale[i - 1], i)) )
    
  morpho_seq[i - 1] ~ dnPhyloCTMC(tree = fbd_tree, Q = Q[i - 1], nSites = n_sites, 
                                  branchRates = clock_morpho[i - 1], type = "Standard")
}

###
# set up final model and run MCMC

# create model object
mymodel = model(fbd_tree)

# model monitor - all numerical parameters
monitors.append( mnModel(filename = "output_combined/combined_evidence_fbd.log", printgen = 100) )

# tree monitor - just the tree
monitors.append( mnFile(filename = "output_combined/combined_evidence_fbd.trees", printgen = 100, fbd_tree) )  

# to the screen 
monitors.append( mnScreen(printgen = 100, root_time, clock_nuclear, clock_morpho) ) 

# create the mcmc object
mymcmc = mcmc(mymodel, monitors, moves, nruns = 2, combine = "mixed")

# and run it
mymcmc.run(generations = 50000, tuningInterval = 200, checkpointInterval = 1000, checkpointFile = "output_combined/combined_evidence_fbd.state")

# quit
q()
